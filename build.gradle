plugins {
    id 'java'
}

group = 'com.nchroniaris'
version = '1.0.0'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {

    mavenCentral()
    mavenLocal()

    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        content {
            includeGroup 'org.spigotmc'
        }
    }

    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }

    maven {
        name = 'codemc-repo'
        url = 'https://repo.codemc.org/repository/maven-public/'
    }

    // For gson-extras
    maven {
        name = "cronapp-repo"
        url = "https://artifactory.cronapp.io/public-release/"
    }

    maven {
        name = 'jitpack-repo'
        url = 'https://jitpack.io'
    }

}

dependencies {

    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compileOnly 'org.jetbrains:annotations:21.0.1'

    // https://www.spigotmc.org/wiki/spigot-gradle/
    compileOnly 'org.spigotmc:spigot-api:1.14.4-R0.1-SNAPSHOT'

    // https://www.spigotmc.org/resources/nbt-api.7939/
    compileOnly 'de.tr7zw:item-nbt-api-plugin:2.7.1'

    // https://dev.bukkit.org/projects/signshop/
    // This is a locally installed jar. I used https://maven.apache.org/guides/mini/guide-3rd-party-jars-local.html to install it to mavenLocal
    compileOnly 'org.makershaven:SignShop:3.5.0'

    // https://jitpack.io/#Tardnicus/MCStonks
    implementation 'com.github.Tardnicus:MCStonks:2.0.0'

}

jar {

    // https://stackoverflow.com/a/49284432
    // As far as I understand this creates a jar file with all the dependencies included in it. Since this program is meant to be distributed and used as a standalone jar (Java is the only "dependency" required) I believe it is a reasonable solution.
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter {
            // https://www.spigotmc.org/threads/gradle-token-replacement.505401/
            it.replace('${project.version}', project.version)
        }
    }
}
