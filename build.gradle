plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group = 'com.nchroniaris'
version = '1.0.0'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {

    mavenCentral()
    mavenLocal()

    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        content {
            includeGroup 'org.spigotmc'
        }
    }

    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }

    // For NBTAPI
    maven {
        name = 'codemc-repo'
        url = 'https://repo.codemc.org/repository/maven-public/'
    }

    // For gson-extras, included in MCStonks
    maven {
        name = 'cronapp-repo'
        url = 'https://artifactory.cronapp.io/public-release/'
    }

    // For commodore, since it uses Mojang's Brigadier library
    maven {
        name = 'minecraft-libraries'
        url = 'https://libraries.minecraft.net/'
    }

    // For MCStonks
    maven {
        name = 'jitpack-repo'
        url = 'https://jitpack.io'
    }

}

dependencies {

    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compileOnly 'org.jetbrains:annotations:21.0.1'

    // https://www.spigotmc.org/wiki/spigot-gradle/
    compileOnly 'org.spigotmc:spigot-api:1.14.4-R0.1-SNAPSHOT'

    // https://www.spigotmc.org/resources/nbt-api.7939/
    compileOnly 'de.tr7zw:item-nbt-api-plugin:2.7.1'

    // https://dev.bukkit.org/projects/signshop/
    // This is a locally installed jar. I used https://maven.apache.org/guides/mini/guide-3rd-party-jars-local.html to install it to mavenLocal
    compileOnly 'org.makershaven:SignShop:3.5.0'

    // https://mvnrepository.com/artifact/me.lucko/commodore
    implementation 'me.lucko:commodore:1.9'

    // https://jitpack.io/#Tardnicus/MCStonks
    implementation 'com.github.Tardnicus:MCStonks:2.0.0'

}

shadowJar {

    // Include stuff ONLY from the runtime classpath -- i.e. only the "implementation" dependencies
    configurations = [project.configurations.runtimeClasspath]

    archiveClassifier.set('FAT')

    dependencies {
        exclude dependency('com.mojang:brigadier')
    }

}

processResources {

    // After begrudgingly trying to understand how these tasks interact, it seems that processResources will complain about duplicates, since there is one copy before the replacement and one after. I don't know why the first one still exists, so perhaps I am doing something else wrong. To be honest, I have no idea what I am doing, but this seems to work ¯\_(ツ)_/¯.
    duplicatesStrategy DuplicatesStrategy.INCLUDE

    from(sourceSets.main.resources.srcDirs) {
        filter {
            // https://www.spigotmc.org/threads/gradle-token-replacement.505401/
            it.replace('${project.version}', project.version)
        }
    }

}
